------------------------------------------------------------------
-- Переключение состояние счета кассы между состоянием ДЕНЬ/ВЕЧЕР
------------------------------------------------------------------
procedure EVENIN_KASS_SWITCH_DPFC(p_operation_id      in operation.id%type,
                                  p_object_id         in compound_action.compound_document_id%type,
                                  p_additional_params in varchar2 default null) is
  cursor div_kass(c_id pls_integer) is
    select d.is_evening_cash
      from division d, account a
     where a.division_id = d.id
       and substr(a.account_number, 1, 4) in ('1001', '1002')
       and a.id = c_id;
  division_kass div_kass%rowtype;

  m_account         p_account.var_account%type;
  tek_div           division.id%type; --ID отделения запустившего операцию пользователя                
  acc_div           division.id%type;
  str_sost_kass     varchar(10); -- текущее состояние кассы словами
  new_str_sost_kass varchar(10); -- новое состояние кассы словами
  m_YESNO           varchar(3);
  role_enable       varchar(1);
begin
  --выдана ли нужная роль
  select P_SR_SECURITY.get_is_role_granted('FC_ACCOUNT_KASSA')
    into role_enable
    from dual;
  if role_enable = 'F' then
    p_exception.raise_common_except('У Вас нет прав на переключение режима кассы! (role FC_ACCOUNT_KASSA)');
  end if;
  --получаем информацию по счету, на котором запущена оп-я
  m_account := p_account.get_account_params(p_id => p_object_id);
  --получаем состояние кассы (день-вечер) того счета, на котором запущена операция
  open div_kass(p_object_id);
  fetch div_kass
    into division_kass;
  close div_kass;
  if division_kass.is_evening_cash = 'T' then
    str_sost_kass := 'Вечерняя';
  else
    str_sost_kass := 'Дневная';
  end if;
  -- ID отделения пользователя, запустившего операцию
  tek_div := p_sr_security.get_current_division_id;

  if substr(m_account.account_number, 1, 1) <> '1' then
    p_exception.raise_common_except('Выбран не кассовый счет!');
  end if;

  if tek_div <> m_account.division_id then
    p_exception.raise_common_except('Вы не можете переключать кассу не своего отделения!');
  end if;

  p_user_output.put_line_fmt('Текущее состояние - %s',
                             str_sost_kass);
  -- Получаем значение из параметра операции
  m_YESNO := p_params_translator.get_str_param(p_additional_params,
                                               'status');
  if UPPER(m_YESNO) = 'ДА' then
    new_str_sost_kass := 'ВЕЧЕРНЯЯ';
  else
    new_str_sost_kass := 'ДНЕВНАЯ';
  end if;
  p_user_output.put_line_fmt('Выбрано новое состояние-%s',
                             new_str_sost_kass);

  if UPPER(m_YESNO) = 'ДА' and division_kass.is_evening_cash = 'T' then
    p_user_output.put_line_fmt('Состояние кассы "Вечерняя". Переключать нечего!');
  elsif UPPER(m_YESNO) = 'ДА' and division_kass.is_evening_cash = 'F' then
    update division d set d.is_evening_cash = 'T' where d.id = tek_div;
  elsif UPPER(m_YESNO) = 'НЕТ' and division_kass.is_evening_cash = 'F' then
    p_user_output.put_line_fmt('Состояние кассы "Дневная". Переключать нечего!');
  elsif UPPER(m_YESNO) = 'НЕТ' and division_kass.is_evening_cash = 'T' then
    update division d set d.is_evening_cash = 'F' where d.id = tek_div;
  
  end if;
end;
